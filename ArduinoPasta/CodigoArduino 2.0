#include <Servo.h>

Servo servoMotor;

const int SERVO_PIN = 10;
const int TEMP_PIN  = A2;

// ===== FILA =====
#define MAX_FILA 100
String fila[MAX_FILA];
int inicioFila = 0;
int fimFila = 0;

// ===== SERVO =====
unsigned long timer = 0;
bool executando = false;
bool voltando = false;
String comandoAtual = "";

// ===== TEMPERATURA =====
int TempRaw = 0;
float TempC = 0;

void setup() {
  Serial.begin(9600);
  servoMotor.attach(SERVO_PIN);
  servoMotor.write(90);
  Serial.println("Pronto.");
}


bool filaVazia() {
  return inicioFila == fimFila;
}

void adicionarFila(String cmd) {
  int prox = (fimFila + 1) % MAX_FILA;
  if (prox != inicioFila) {
    fila[fimFila] = cmd;
    fimFila = prox;
  }
}

String pegarFila() {
  if (!filaVazia()) {
    String cmd = fila[inicioFila];
    inicioFila = (inicioFila + 1) % MAX_FILA;
    return cmd;
  }
  return "";
}

void loop() {

  // ===== TEMPERATURA =====
  TempRaw = analogRead(TEMP_PIN);
  TempC = TempRaw * (5.0 / 1023.0) * 100.0;

  Serial.print("TEMP:");
  Serial.println(TempC);

  // ===== RECEBE TODOS OS COMANDOS DO PYTHON =====
  while (Serial.available() > 0) {
    String cmd = Serial.readStringUntil('\n');
    cmd.trim();
    adicionarFila(cmd);
  }

  
  if (!executando && !filaVazia()) {
    comandoAtual = pegarFila();
    executando = true;
    voltando = false;
    timer = millis();

    if (comandoAtual == "mais") {
      servoMotor.write(120);
      Serial.println("MAIS");
    } 
    else if (comandoAtual == "menos") {
      servoMotor.write(60);
      Serial.println("MENOS");
    }
  }

  // ===== CONTROLE DE TEMPO DO MOVIMENTO =====
  if (executando) {

    // Tempo no extremo
    if (!voltando && millis() - timer >= 500) {
      servoMotor.write(90);
      voltando = true;
      timer = millis();
    }


    else if (voltando && millis() - timer >= 300) {
      executando = false;
      voltando = false;
    }
  }
}
